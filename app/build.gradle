apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "de.dbaelz.onofftracker"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 5
        versionName "1.2.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        dev {
            dimension "environment"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "On-Off Tracker dev"
        }
        prod {
            dimension "environment"
            resValue "string", "app_name", "On-Off Tracker"
        }
    }
}

ext {
    supportLibVersion = '26.1.0'
    daggerVersion = 2.13
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    compile "com.j256.ormlite:ormlite-android:5.0"
    compile "net.danlew:android.joda:2.9.4.2"
    compile "com.github.PhilJay:MPAndroidChart:v2.2.5"

    compile "io.reactivex:rxjava:1.1.6"
    compile "io.reactivex:rxandroid:1.2.1"
}

// Signing the APK with the OnOff-Tracker.signing property and external config files
if (project.hasProperty("OnOff-Tracker.signing")) {
    String signingFile = project.property("OnOff-Tracker.signing") + "/OnOff-Tracker.signing";
    if (new File(signingFile).exists()) {
        apply from: signingFile;
    }
}